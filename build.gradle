allprojects {
    // Top-level build file where you can add configuration options common to all sub-projects/modules.
    ext {
        // Author
        developerId = "davideas"
        developerName = "Davide Steduto"
        developerEmail = "dave.dna@gmail.com"

        // Library Repository
        libraryName = "FlexibleAdapter"
        bintrayRepo = "maven"
        packageExt = "aar"
        siteUrl = "https://github.com/davideas/FlexibleAdapter"
        gitUrl = "https://github.com/davideas/FlexibleAdapter.git"

        // SDK and Build tools version
        minSdk = 14
        targetSdk = 26
        buildTools = "26.0.2"

        // Android Architecture Components
        archLifecycleVer = "1.0.3"
        archExtensionsVer = "1.0.0-rc1"
        archPersistenceVer = archExtensionsVer
        archLib = [
                lifecycle      : "android.arch.lifecycle:runtime:${archLifecycleVer}",
                extensions     : "android.arch.lifecycle:extensions:${archExtensionsVer}",
                compiler       : "android.arch.lifecycle:compiler:${archExtensionsVer}",
                commonJava8    : "android.arch.lifecycle:common-java8:${archExtensionsVer}",
                reactiveStreams: "android.arch.lifecycle:reactivestreams:${archExtensionsVer}",
                coreTesting    : "android.arch.core:core-testing:${archExtensionsVer}",
                room           : "android.arch.persistence.room:runtime:${archPersistenceVer}",
                roomCompiler   : "android.arch.persistence.room:compiler:${archPersistenceVer}",
                roomRxJava2    : "android.arch.persistence.room:rxjava2:${archPersistenceVer}"
        ]

        // Support Libraries dependencies
        supportVer = "27.0.0"
        constraintVer = "1.1.0-beta2"
        supportLib = [
                design          : "com.android.support:design:${supportVer}",
                recyclerView    : "com.android.support:recyclerview-v7:${supportVer}",
                cardView        : "com.android.support:cardview-v7:${supportVer}",
                appcompat       : "com.android.support:appcompat-v7:${supportVer}",
                customTabs      : "com.android.support:customtabs:${supportVer}",
                support_v4      : "com.android.support:support-v4:${supportVer}",
                support_v13     : "com.android.support:support-v13:${supportVer}",
                annotations     : "com.android.support:support-annotations:${supportVer}",
                vectorDrawable  : "com.android.support:support-vector-drawable:${supportVer}",
                preference_v7   : "com.android.support:preference-v7:${supportVer}",
                preference_v14  : "com.android.support:preference-v14:${supportVer}",
                constraintLayout: "com.android.support.constraint:constraint-layout:${constraintVer}",
        ]

        // Widgets
        flipViewVer = "1.1.3@aar"
        iconicsVer = "2.9.3@aar"
        iconicsCmdVer = "1.9.32.2@aar"
        widgets = [
                flipView        : "eu.davidea:flipview:${flipViewVer}",
                iconicsCore     : "com.mikepenz:iconics-core:${iconicsVer}",
                iconicsViews    : "com.mikepenz:iconics-views:${iconicsVer}",
                iconicsCommunity: "com.mikepenz:community-material-typeface:${iconicsCmdVer}",
        ]

        // 3rd Libraries
        butterKnifeVer = "8.8.1"
        timberVer = "4.5.1"
        glideVer = "4.1.1"
        javaxInjectVer = "1"
        junitPlatformVer = "1.0.1"
        junitJupiterVer = "5.0.1"
        libraries = [
                butterKnife        : "com.jakewharton:butterknife:${butterKnifeVer}",
                butterKnifeCompiler: "com.jakewharton:butterknife-compiler:${butterKnifeVer}",
                timber             : "com.jakewharton.timber:timber:${timberVer}",
                glide              : "com.github.bumptech.glide:glide:${glideVer}",
                glideCompiler      : "com.github.bumptech.glide:compiler:${glideVer}",
                javaxInject        : "javax.inject:javax.inject:${javaxInjectVer}",
        ]

        // License stuff
        licenseName = "The Apache Software License, Version 2.0"
        licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        licenseDist = "repo"
        allLicenses = ["Apache-2.0"]
    }

    repositories {
        jcenter()
        google()
        maven { url "http://dl.bintray.com/davideas/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }
}

buildscript {
    repositories {
        jcenter()
        google()
        maven { url "http://dl.bintray.com/davideas/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0-alpha02'
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
        classpath "com.github.dcendents:android-maven-gradle-plugin:1.5"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.1"
        classpath "com.vanniktech:gradle-android-javadoc-plugin:0.2.1"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.0.0"
        classpath "eu.davidea:grabver:0.6.0"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}