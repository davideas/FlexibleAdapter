allprojects {
    // Top-level build file where you can add configuration options common to all sub-projects/modules.
    ext {
        // Author
        developerId = "davideas"
        developerName = "Davide Steduto"
        developerEmail = "dave.dna@gmail.com"

        // Library Repository
        libraryName = "FlexibleAdapter"
        bintrayRepo = "maven"
        packageExt = "aar"
        siteUrl = "https://github.com/davideas/FlexibleAdapter"
        gitUrl = "https://github.com/davideas/FlexibleAdapter.git"

        // SDK and Build tools version
        minSdk = 14
        targetSdk = 27
        compileSdk = 'android-P'
        buildTools = "27.0.3"

        // Android Architecture Components
        archLifecycleVer = "2.0.0-alpha1"
        archExtensionsVer = "2.0.0-alpha1"
        roomPersistenceVer = "1.1.0-beta2"
        archLib = [
                lifecycle      : "androidx.lifecycle:lifecycle-runtime:${archLifecycleVer}",
                extensions     : "androidx.lifecycle:lifecycle-extensions:${archExtensionsVer}",
                compiler       : "androidx.lifecycle:lifecycle-compiler:${archExtensionsVer}",
                commonJava8    : "androidx.lifecycle:lifecycle-common-java8:${archExtensionsVer}",
                reactiveStreams: "androidx.lifecycle:lifecycle-reactivestreams:${archExtensionsVer}",
                coreTesting    : "androidx.arch.core:core-testing:${archExtensionsVer}",
                room           : "androidx.room:room-runtime:${roomPersistenceVer}",
                roomCompiler   : "androidx.room:room-compiler:${roomPersistenceVer}",
                roomRxJava2    : "androidx.room:room-rxjava2:${roomPersistenceVer}"
        ]

        // Support Libraries dependencies
        supportVer = "1.0.0-alpha1"
        constraintVer = "1.1.0"
        supportLib = [
                design          : "com.google.android.material:material:${supportVer}",
                recyclerView    : "androidx.recyclerview:recyclerview:${supportVer}",
                cardView        : "androidx.cardview:cardview:${supportVer}",
                appCompat       : "androidx.appcompat:appcompat:${supportVer}",
                customTabs      : "androidx.browser:browser:${supportVer}",
                support_v4      : "androidx.legacy:legacy-support-v4:${supportVer}",
                support_v13     : "androidx.legacy:legacy-support-v13:${supportVer}",
                annotations     : "androidx.annotation:annotation:${supportVer}",
                vectorDrawable  : "androidx.vectordrawable:vectordrawable:${supportVer}",
                preference_v7   : "androidx.preference:preference:${supportVer}",
                preference_v14  : "androidx.legacy:legacy-preference-v14:${supportVer}",
                constraintLayout: "androidx.constraintlayout:constraintlayout:${constraintVer}",
        ]

        // Widgets
        flipViewVer = "1.1.3@aar"
        iconicsVer = "3.0.3@aar"
        iconicsCmdVer = "2.0.46.1@aar"
        widgets = [
                flipView        : "eu.davidea:flipview:${flipViewVer}",
                iconicsCore     : "com.mikepenz:iconics-core:${iconicsVer}",
                iconicsViews    : "com.mikepenz:iconics-views:${iconicsVer}",
                iconicsCommunity: "com.mikepenz:community-material-typeface:${iconicsCmdVer}",
        ]

        // 3rd Libraries
        timberVer = "4.7.0"
        glideVer = "4.7.1"
        javaxInjectVer = "1"
        junitPlatformVer = "1.1.0"
        junitJupiterVer = "5.1.0"
        libraries = [
                timber             : "com.jakewharton.timber:timber:${timberVer}",
                glide              : "com.github.bumptech.glide:glide:${glideVer}",
                glideCompiler      : "com.github.bumptech.glide:compiler:${glideVer}",
                javaxInject        : "javax.inject:javax.inject:${javaxInjectVer}",
        ]

        // License stuff
        licenseName = "The Apache Software License, Version 2.0"
        licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        licenseDist = "repo"
        allLicenses = ["Apache-2.0"]
    }

    repositories {
        jcenter()
        google()
        maven { url "http://dl.bintray.com/davideas/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
        options.addStringOption('encoding', 'UTF-8')
    }
}

buildscript {
    repositories {
        jcenter()
        google()
        maven { url 'https://jitpack.io' }
        maven { url "http://dl.bintray.com/davideas/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-alpha14'
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.0.1"
        classpath "com.vanniktech:gradle-android-javadoc-plugin:0.3.0-SNAPSHOT"
        classpath "eu.davidea:grabver:0.7.0"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}