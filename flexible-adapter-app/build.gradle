apply plugin: 'com.android.application'
apply plugin: 'eu.davidea.grabver'

versioning {
	major 5
	minor 0
	preRelease "SNAPSHOT"
}

android {
	compileSdkVersion targetSdk
	buildToolsVersion buildTools

	defaultConfig {
		applicationId 'eu.davidea.samples.flexibleadapter'
		minSdkVersion minSdk
		targetSdkVersion targetSdk
		versionCode versioning.code
		versionName versioning.name

		testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

		applicationVariants.all { variant ->
			variant.outputs.each { output ->
				def file = output.outputFile
				def fileName = 'flexible-adapter-v' + versioning.name + '-c' + versioning.code + '.apk'
				output.outputFile = new File(file.parentFile, fileName)
			}
		}
	}
	buildTypes {
		debug {
			versionNameSuffix ' #' + versioning.build + versioning.builtOn
			minifyEnabled false
		}
		release {
			zipAlignEnabled true
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
	testOptions {
		unitTests.returnDefaultValues = true
	}

	dataBinding {
		enabled = true
	}
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])

	// Testing
	androidTestCompile supportDependencies.annotations
	androidTestCompile 'com.android.support.test:runner:0.5'
	androidTestCompile 'com.android.support.test:rules:0.5'
	androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
		exclude group: 'com.android.support', module: 'support-annotations'
	})
	testCompile 'org.robolectric:robolectric:3.3.2'
	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-core:2.7.22'

	// FlexibleAdapter + extensions
	compile project (':flexible-adapter')
	compile project (':flexible-adapter-ui')
	compile project (':flexible-adapter-databinding')

	// FlipView
	compile 'eu.davidea:flipview:1.1.3'

	// ButterKnife
	compile 'com.jakewharton:butterknife:8.5.1'
	annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'

	// Support Libraries
	compile supportDependencies.appcompat
	compile supportDependencies.cardview
	compile (supportDependencies.design) {
		exclude module: 'support-v4'
		exclude module: 'support-annotations'
	}
	compile (supportDependencies.recyclerview) {
		exclude module: 'support-v4';
		exclude module: 'support-annotations'
	}

	// Glide (Image Loader)
	compile 'com.github.bumptech.glide:glide:3.7.0'

	// Smooth-app-bar-layout
	compile ('me.henrytao:smooth-app-bar-layout:24.1.1.0') {
		exclude group: 'com.android.support'
	}

}